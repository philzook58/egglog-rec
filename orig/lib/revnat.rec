REC-SPEC Revnat

# common definitions to be included in the REC/revnat*.rec benchmarks

SORTS
  Nat List
CONS
  l : Nat List -> List
  nil : -> List
  d0 : -> Nat              #---- zero
  s : Nat -> Nat          #---- successor
OPNS
  d10 : -> Nat             #---- 10
  plus : Nat Nat -> Nat   #---- addition
  times : Nat Nat -> Nat  #---- product
  gen : Nat -> List
  conc : List List -> List
  rev : List -> List
VARS
  E N M : Nat
  L1 L2 : List
RULES
  d10 -> s(s(s(s(s(s(s(s(s(s(d0))))))))))
  plus(d0, N) -> N
  plus(s(N), M) -> s(plus(N, M))
  times(d0, N) -> d0
  times(s(N), M) -> plus(M, times(N, M))
  gen(s(N)) -> l(s(N), gen(N))
  gen(d0) -> l(d0, nil)
  conc(l(E, L1), L2) -> l(E, conc(L1, L2))
  conc(nil, L2) -> L2
  rev(l(E, L1)) -> conc(rev(L1), l(E, nil))
  rev(nil) -> nil
EVAL
END-SPEC
